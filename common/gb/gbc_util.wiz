import "gb";
import "banks";

namespace gbc_util {
    in hram {
        var gbc_detected : u8;
        var gba_detected : u8;
    }

    // Must be called near the very start of the program (relies on startup state of a and b registers)
    // After:
    //      gbc_detected = 1 if running on GBC-compatible hardware, 0 otherwise.
    //      gba_detected = 1 if running on a GBA, 0 otherwise.
    func detect() {
        if a == 0x11 {
            gba_detected = a = b & 1;
            gbc_detected = a = 1;
        } else {
            a = 0;
            gba_detected = a;
            gbc_detected = a;
        }
    }

    // Arguments: b = desired speed setting (gb.color.SPEED_MASK_TURBO_ON or gb.color.SPEED_MASK_TURBO_OFF)
    func set_speed(desired_speed : u8 in b) {
        if { a = gbc_detected; } && a != 0 {            
            if { a = gb.color.speed & b; } && a != b {
                gb.joy.ctrl = a = 0x30;
                gb.color.speed = a = gb.color.SPEED_MASK_SWITCH;
                stop();
            }
        }
    }
}