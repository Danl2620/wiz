import "gb";
import "banks";

in ram {
    namespace stack {
        let SIZE = 256;
        var data : [u8; SIZE];
    }
}

in rom @ 0x150 {
    import "memory";
    import "joy";
    import "video";
    import "gbc_util";
    import "sgb_util";

    func main() {
        interrupt = false;
        hl = &stack.data as u16 + stack.SIZE - 1;
        sp = hl;

        gbc_util.detect();
        gbc_util.set_speed(gb.color.SPEED_MASK_TURBO_ON);
        inline memset(0xC000 as *u8, 0, 0x2000);

        // Turn off the screen.
        sgb_util.detect();
        video.init();
        video.disable();

        joy.init();

        memset(&gb.ram.vram[0], 0, sizeof(typeof(gb.ram.vram)));

        if { a = sgb_util.sgb_detected; } && a != 0 {
            // Running on a Super Game Boy.

            // Enable SGB mask.
            sgb_util.send_packet(&sgb_mask_enable[0]);
            // Copy the SNES program into GB tileset memory.
            memcpy(&gb.ram.tile[0], &snes_main[0], sizeof(typeof(snes_main)));
            // Layout the tilemap to do a VRAM transfer via SGB packet.
            sgb_util.prepare_vram_transfer_offscreen();
            // Send the packet sequence to start the upload sequence.
            sgb_util.send_packet_sequence(&sgb_upload_sequence[0]);

            // NOTE: If we were uploading a larger SNES program, it would likely require multiple VRAM sends.
            // We would need turn off the screen again, load the next 4K of data, then do another transfer, and only do the JUMP at the very end.
            // We would probably want to manually construct the DATA_SND packets and send single packets rather than using hardcoded packet sequences in ROM.
            debug_break();
        } else {
            // Not running on a Super Game Boy.
            // Turn the screen back on, with backgrounds enabled using tileset 2.
            gb.mono.palette.bg = a = 0b11100100;
            gb.mono.palette.obj1 = a = 0b11100000;
            gb.mono.palette.obj2 = a = 0b01101011;
            gb.lcd.ctrl = a = gb.lcd.CTRL_ON | gb.lcd.CTRL_TILESET2 | gb.lcd.CTRL_BG_ON;
        }

        // Enable some basic interrupts.
        gb.interrupt.flag = a = 0;
        gb.interrupt.enable = a = gb.interrupt.VBLANK | gb.interrupt.STAT;
        gb.lcd.stat = a = gb.lcd.STAT_SCANLINE_ENABLE;
        interrupt = true;

        while true {
            //debug_break();
            joy.update();
            video.request_update();
        }
    }

    #[irq]
    func draw() {
        video.acknowledge_update();
        hl = pop();
        de = pop();
        bc = pop();
        af = pop();
    }

    #[irq]
    func stub() {
        hl = pop();
        de = pop();
        bc = pop();
        af = pop();
    }

    let stat = stub;
    let timer = stub;
    let serial = stub;
    let joypad = stub;

    // Helper function to create a single 16-byte SGB packet.
    let SGB_PACKET(
        b0, b1, b2, b3, b4, b5, b6, b7, b8, b9, b10, b11, b12, b13, b14, b15
    ) = [b0 | 1 as u8, b1, b2, b3, b4, b5, b6, b7, b8, b9, b10, b11, b12, b13, b14, b15];

    const sgb_mask_enable = SGB_PACKET(gb.super.MASK_EN, 2, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);

    const sgb_upload_sequence : [u8]
        // Transfer a 4K SNES program into RAM in bank 0x7F.
        = SGB_PACKET(
            gb.super.DATA_TRN,
            0x00, 0x00, 0x7F,
            0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
        // Mute sound output from the SNES APU.
        // (Also enables IPL ROM on the SPC, allowing to custom SPC700 code to be uploaded.)
        ~ SGB_PACKET(
            gb.super.SOUND,
            0, 0, 0, 0xFE,
            0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
        // Jump to the uploaded code, using two fixed-address entry points
        // at the start of uploaded data to jump to a main routine + NMI routine.
        ~ SGB_PACKET(
            gb.super.JUMP,
            0x00, 0x00, 0x7F,
            0x03, 0x00, 0x7F,
            0, 0, 0, 0, 0, 0, 0, 0, 0)
        ~ [0xFF];

    const snes_main = embed "snes_main.bin";
}