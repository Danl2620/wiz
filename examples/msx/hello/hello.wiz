import "msx";

bank rom @ 0x4000 : [constdata; 0x4000];
bank ram @ 0xE000 : [vardata; 0x2000];

in ram {}

in rom {
    namespace header {
        const id = "AB";
        const init_handler = main;
        const statement_handler = 0u16;
        const expansion_handler = 0u16;
        const basic_start = 0u16;
        const padding = [0u8; 6];
    }

    #[fallthrough] func main() {
        interrupt = false;

        // Set border color.
        msx.bios.border_color = a = msx.vdp.color.BLACK;
        msx.bios.vdp_apply_colors();

        // Init screen mode.
        load_inc_repeat(
            de = <>:&msx.bios.screen_2_nametable,
            hl = <>:&screen_init_data,
            bc = sizeof(typeof(screen_init_data)));
        msx.bios.init_screen_2(
            msx.bios.screen_2_nametable,
            msx.bios.screen_2_tile_color_table,
            msx.bios.screen_2_tile_pattern_table,
            msx.bios.screen_2_sprite_attribute_table,
            msx.bios.screen_2_sprite_pattern_table
        );

        // Disable the screen so we can access VRAM. (Initializing the screen mode also enables the screen).
        msx.bios.disable_screen();
        // Load background colors.
        msx.bios.vdp_vram_fill_block(0x2000, (msx.vdp.color.WHITE << 4) | msx.vdp.color.LIGHT_BLUE, 0x1800);
        // Load background tileset.
        msx.bios.vdp_vram_write_block(0x0000, &bkg_tileset[0], sizeof(typeof(bkg_tileset)));
        msx.bios.vdp_vram_write_block(0x0800, &bkg_tileset[0], sizeof(typeof(bkg_tileset)));
        msx.bios.vdp_vram_write_block(0x1000, &bkg_tileset[0], sizeof(typeof(bkg_tileset)));
        // Clear nametable.
        msx.bios.vdp_vram_fill_block(0x1800, 0, 0x300);
        // Write text to nametable.
        msx.bios.vdp_vram_write_block(0x190A, &message[0], sizeof(typeof(message)));
        // Turn screen on.
        msx.bios.enable_screen();

        interrupt = true;

        while true {
            halt();
        }
    }

    const screen_init_data : [u16] = [
        0x1800,
        0x2000,
        0x0000,
        0x1B00,
        0x3800,
    ];

    const message = "HELLO  WORLD";
    const bkg_tileset = embed "hello_tiles.chr";
}