import "banks";
import "globals";
import "utility";
import "entity_definitions";

namespace entity {
    in zeropage {
        var current_index : u8 in b14;
    }

    in ram {
        let COUNT = 16;

        let PLAYER_INDEX = 0;
        let PLAYER_COUNT = 1;
        let ENEMY_INDEX = 1;
        let ENEMY_COUNT = 15;

        var type : [u8; COUNT];
        var flags : [u8; COUNT];
        var x_subpixel : [u8; COUNT];
        var x_pixel : [u8; COUNT];
        var x_screen : [u8; COUNT];
        var y_subpixel : [u8; COUNT];
        var y_pixel : [u8; COUNT];
        var y_screen : [u8; COUNT];
        var hp : [u8; COUNT];        
        var flash_timer : [u8; COUNT];
        var v0 : [u8; COUNT];
        var v1 : [u8; COUNT];
        var v2 : [u8; COUNT];
        var v3 : [u8; COUNT];
        var v4 : [u8; COUNT];
        var v5 : [u8; COUNT];
        var v6 : [u8; COUNT];
        var v7 : [u8; COUNT];
    }

    func init_system() {
        var index : u8 in x;

        a = 0;

        for index in 0 .. COUNT - 1 {
            type[index] = a;
            flags[index] = a;
            x_subpixel[index] = a;
            x_pixel[index] = a;
            x_screen[index] = a;
            y_subpixel[index] = a;
            y_pixel[index] = a;
            y_screen[index] = a;
            hp[index] = a;
            flash_timer[index] = a;
            v0[index] = a;
            v1[index] = a;
            v2[index] = a;
            v3[index] = a;
            v4[index] = a;
            v5[index] = a;
            v6[index] = a;
            v7[index] = a;
        }
    }

    func init_entity(
        index : u8 in x,
        spawn_type : u8 in y,
        spawn_x : u16 in w0,
        spawn_y : u16 in w2
    ) {
        flags[index] = a = 0;
        x_subpixel[index] = a;
        y_subpixel[index] = a;
        hp[index] = a;
        flash_timer[index] = a;
        v0[index] = a;
        v1[index] = a;
        v2[index] = a;
        v3[index] = a;
        v4[index] = a;
        v5[index] = a;
        v6[index] = a;
        v7[index] = a;
        x_pixel[index] = a = <:spawn_x;
        x_screen[index] = a = >:spawn_x;
        y_pixel[index] = a = <:spawn_y;
        y_screen[index] = a = >:spawn_y;

        type[index] = a = spawn_type;

        if !zero {
            var func_ptr : func in call_handler.func_ptr;
            <:func_ptr = a = (&entity_definitions.init_handlers_l as *u8)[spawn_type];
            >:func_ptr = a = (&entity_definitions.init_handlers_h as *u8)[spawn_type];
            return (*(&func_ptr as *func))();
        }
    }

    func spawn_entity(spawn_type : u8 in y, spawn_x : u16 in w0, spawn_y : u16 in w2) : bool in carry {
        var index : u8 in x;

        for index in 0 .. COUNT - 1 {
            a = type[index];
            if zero {
                init_entity(index, spawn_type, spawn_x, spawn_y);
                return true;
            }
        }

        return false;
    }

    func update_all() {
        var index : u8 in x;

        for index in 0 .. COUNT - 1 {
            current_index = index;

            var ent_type : u8 in y;
            ent_type = a = type[index];

            if !zero {
                var func_ptr : func in call_handler.func_ptr;
                <:func_ptr = a = (&entity_definitions.update_handlers_l as *u8)[ent_type];
                >:func_ptr = a = (&entity_definitions.update_handlers_h as *u8)[ent_type];
                call_handler(func_ptr);
            }

            index = current_index;
        }
    }
}